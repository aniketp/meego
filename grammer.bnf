/* Lexical Parser */
!whitespace : '\t' | '\n' | '\r' | ' ' ;
!lineComment : '/' '/' { . } '\n' ;
!blockComment : '/' '*' { . | '*' } '*' '/' ;

_letter : 'A'-'Z' | 'a'-'z' | '_' ;
_digit : '0'-'9' ;
_alpha : _letter | _digit ;

string_literal : '"' {_alpha | ' ' | '!' | '?' }'"' ;
int : '0' | '1'-'9' {_digit} ;

/* Keywords */
func : 'f' 'u' 'n' 'c' ;
let : 'l' 'e' 't' ;
if : 'i' 'f' ;
else : 'e' 'l' 's' 'e' ;
return : 'r' 'e' 't' 'u' 'r' 'n' ;
true : 't' 'r' 'u' 'e' ;
false : 'f' 'a' 'l' 's' 'e' ;
and : 'a' 'n' 'd' ;
for : 'f' 'o' 'r' ;
or : 'o' 'r' ;

ident : _letter {_alpha} ;

/* Punctuation */
plus : '+' ;
minus : '-' ;
mul : '*' ;
div : '/' ;

eq : '=' '=' ;
neq : '!' '=' ;
lt : '<' ;
gt : '>' ;

lbrace : '{' ;
rbrace : '}' ;
assign : '=' ;
lparen : '(' ;
rparen : ')' ;
comma : ',' ;
semicolon : ';' ;
// period : '.' ;
// colon : ':' ;


/* Syntactic Parser */
<<
import (
	"github.com/aniketp/go-compiler/ast"
)
>>

Program
	: Functions Statements << ast.NewProgram($0, $1) >>
	;

Functions
	: Functions Function << ast.AppendStatement($0, $1) >>
	| empty << ast.NewStatementList() >>
	;

Statements
	: Statements Statement << ast.AppendStatement($0, $1) >>
	| empty << ast.NewStatementList() >>
	;

Function
	: func ident lparen FormalArgs rparen ident StatementBlock << ast.NewFunctionStatement($1, $3, $5, $6) >>
	;

Statement
	: if Expression StatementBlock IfStatement << ast.NewIfStatement($1, $2, $3) >>
//  | for Expression StatementBlock 
	| ident assign Expression semicolon << ast.NewAssignStatement($0, $2) >>
	| let ident assign Expression semicolon << ast.NewIdentInit($1, $3) >>
	| Expression semicolon << ast.NewExpressionStatement($0) >>
	| return Expression semicolon << ast.NewReturnStatement($1) >>
	;

IfStatement
	: else StatementBlock << $1, nil >>
	| empty
	;

StatementBlock
	: lbrace Statement rbrace << ast.NewBlockStatement($1) >>
	;

Expression
	: Expression Operation Term
	| Term
	;

Operation
	: plus
	| minus
	| lt
	| gt
	| eq
	| neq
	| and
	| or
	;

Term
	: Term mul Factor
	| Term div Factor
	| string_literal
	| Bool
	| Factor
	;

Factor
	: lparen Expression rparen
	| int
	| ident
	| ident lparen Args rparen
	| error
	;

Bool
	: true
	| false
	;

Args
	: Expression ArgsList
	| empty
	;

ArgsList
	: ArgsList comma Expression
	| empty
	;

FormalArgs
	: ident ident FormalArgsList
	| empty
	;

FormalArgsList
	: FormalArgsList comma ident ident
	| empty
	;